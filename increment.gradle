def getStoredVersionCode(Project project, String versionFileName = 'version.properties', String paramName = 'incrementVersion') {
    boolean increment = false;
    if (project.hasProperty(paramName)) {
        increment = Boolean.parseBoolean(project.getProperties().get(paramName));
    }
    def versionPropsFile = file(versionFileName)
    def Properties versionProps = new Properties()
    def code = 0
    if (versionPropsFile.canRead()) {
        versionProps.load(new FileInputStream(versionPropsFile))
        code = versionProps['VERSION_CODE'].toInteger()
    }
    if (increment) {
        println "Incrementing stored version code"
        code++
        versionProps['VERSION_CODE'] = code.toString()
        versionProps.store(versionPropsFile.newWriter(), null)
    }
    println "Using version code = " + code
    return code
}

ext {
    getStoredVersionCode = this.&getStoredVersionCode
}